#----------------------------- General vars --------------------------------------
pkgname=nspr
pkgver=4.9.6
pkgbuild=1
arch=('auto')

shortdesc="Netscape Portable Runtime"
longdesc=("This library provides platform independent non-GUI operating system facilities")
source=("ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${pkgname}-${pkgver}.tar.gz")
tags="net-libs libs"
build_deps="glibc-solibs"
conflicts="seamonkey-solibs"


build()
{
  go_src_dir
  burn_patches
  [ "$ARCH" = "x86_64" ] && confflags="--enable-64bit"
  unset CFLAGS
  unset CXXFLAGS
  ./mozilla/nsprpub/configure \
      --prefix=/usr \
      --libdir=/usr/lib${LIBDIRSUFFIX} \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug ${confflags} || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  NSPR_LIBS=`./config/nspr-config --libs`
  NSPR_CFLAGS=`./config/nspr-config --cflags`
  NSPR_VERSION=`./config/nspr-config --version`
  install -m755 -d "${pkgdir}/usr/lib${LIBDIRSUFFIX}/pkgconfig" || return 1
  sed "${filedir}/nspr.pc.in" -e "s,%libdir%,/usr/lib${LIBDIRSUFFIX}," \
        -e "s,%prefix%,/usr," \
        -e "s,%exec_prefix%,/usr/bin," \
        -e "s,%includedir%,/usr/include/nspr," \
        -e "s,%NSPR_VERSION%,${NSPR_VERSION}," \
        -e "s,%FULL_NSPR_LIBS%,${NSPR_LIBS}," \
        -e "s,%FULL_NSPR_CFLAGS%,${NSPR_CFLAGS}," > "${pkgdir}/usr/lib${LIBDIRSUFFIX}/pkgconfig/nspr.pc" || return 1
  chmod 644 "${pkgdir}/usr/lib${LIBDIRSUFFIX}/pkgconfig/nspr.pc" || return 1
  ln -sf nspr.pc "${pkgdir}/usr/lib${LIBDIRSUFFIX}/pkgconfig/mozilla-nspr.pc" || return 1

  chmod 644 ${pkgdir}/usr/lib${LIBDIRSUFFIX}/*.a || return 1

  rm -rf "${pkgdir}/usr/bin/compile-et.pl" \
        "${pkgdir}/usr/bin/prerr.properties" \
        "${pkgdir}/usr/share/aclocal/nspr.m4" \
        "${pkgdir}/usr/include/nspr/md"


}
